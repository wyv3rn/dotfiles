#!/usr/bin/env nu

# TODO make these globals configurable
let library = "~/.library" | path expand
let viewer = "sioyek"
let browser = "qutebrowser"

def from_bib [path] {
    cat $path | head -n-1 | tail -n+2 | sed 's/=/:/;s/^  //;s/,$//;s/{\(.*\)}/"\1"/' | from yaml
}

def "main open" [
    item: string
] {
    let pdf_file = $"($library)/pdf/($item).pdf"
    let bib_file = $"($library)/bib/($item).bib"
    let bib = from_bib $bib_file

    if ($pdf_file | path exists) {
        nu -c $"($viewer) ($pdf_file)"
    } else if ("url" in $bib) {
        nu -c $"($browser) ($bib.url)"
    } else if ("doi" in $bib) {
        nu -c $"($browser) https://doi.org/($bib.doi)"
    }
}

def "main export" [
    collection: string,
    --short
] {
    mut shorten_sed_cmd = ""
    if $short {
        let short_map = {
            "Proceedings of the ": "",
            "Proceedings on ": "",
        }
        let sed_list = $short_map | transpose from to | each { |e| $"s/($e.from)/($e.to)/" }
        $shorten_sed_cmd = $sed_list | str join ";"
    }
    let collection = open $"($library)/collections/($collection).yml"
    let dst = $collection | get dst
    let bib_files = $collection | get src | each {
        |e| $"($library)/bib/($e).bib"
    }
    awk 'FNR==1 && NR!=1 {print ""} {print}' ...$bib_files | sed $shorten_sed_cmd | save -f $dst
}

def main [] {
    print "No subcommand given"
}

